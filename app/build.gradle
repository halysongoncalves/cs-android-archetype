apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "$rootDir/tools/test-butler.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "br.com.concretesolutions.androidarchetype"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "br.com.concretesolutions.androidarchetype.CustomJUnitRunner"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    signingConfigs {

        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("$rootDir/distribution/debug.keystore")
            storePassword 'android'
        }

        if (System.getenv("KEY_ALIAS") == null) {
            release {}
        } else {
            release {
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled true
            useProguard false
            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }

        qa {
            minifyEnabled false
            useProguard true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            useProguard true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"

    // Local tests deps
    testCompile 'junit:junit:4.12'
    testCompile 'com.jayway.jsonpath:json-path-assert:2.2.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okHttpVersion"
    testCompile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVersion"
    testCompile 'org.robolectric:robolectric:3.1.2'

    // Instrumented tests deps
    androidTestCompile "com.android.support:support-annotations:$rootProject.ext.supportVersion"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.jayway.jsonpath:json-path-assert:2.2.0'
    androidTestCompile "com.linkedin.testbutler:test-butler-library:$rootProject.ext.testButlerVersion"
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okHttpVersion"
    androidTestCompile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVersion"
    androidTestCompile "com.android.support.test:runner:0.5"
}
